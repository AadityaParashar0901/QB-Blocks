$Checking:Off
Function perm2! (X As Long, Y As Long, M As _Unsigned _Byte) Static
    Static As Long h
    h = X * 73856093 Xor Y * 19349663 Xor M + Seed * 193
    h = (h Xor _SHR(h, 13)) * 60493
    h = h Xor _SHR(h, 16)
    perm2! = (h And 2147483647) / 2147483647
End Function
Function perm3! (X As Long, Y As Long, Z As Long, M As _Unsigned _Byte) Static
    Static As _Unsigned Long h
    h = X * 73856093 Xor Y * 19349663 Xor Z * 83492791 Xor M + Seed
    h = (h Xor _SHR(h, 13)) * 60493
    h = h Xor _SHR(h, 16)
    perm3! = (h And 2147483647) / 2147483647
End Function
Function fractal2! (X As Long, Y As Long, S As _Unsigned Long, O As _Unsigned _Byte, M As _Unsigned _Byte) Static
    Static As Double amplitude, total, maxAmplitude, scale
    Static As Single tX, tY
    Static As _Byte i
    Static fX&, fY&, fX1&, fY1&, dX!, n00!, n01!, i1!
    amplitude = 1: total = 0: maxAmplitude = 0: scale = S
    For i = 0 To O
        tX = X / scale
        tY = Y / scale
        fX& = Int(tX)
        fY& = Int(tY)
        dX! = fade!(tX - fX&)
        fX1& = fX& + 1
        fY1& = fY& + 1
        n00! = perm2(fX&, fY&, M)
        n01! = perm2(fX&, fY1&, M)
        i1! = n00! + (perm2(fX1&, fY&, M) - n00!) * dX!
        total = total + amplitude * (i1! + (n01! + (perm2(fX1&, fY1&, M) - n01!) * dX! - i1!) * fade!(tY - fY&))
        maxAmplitude = maxAmplitude + amplitude
        amplitude = amplitude / 2
        scale = scale / 2
    Next i
    fractal2! = total / maxAmplitude
End Function
Function fractal3! (X As Long, Y As Long, Z As Long, S As _Unsigned Long, O As _Unsigned _Byte, M As _Unsigned _Byte) Static
    Static As Double amplitude, total, maxAmplitude, scale
    Static As Single tX, tY, tZ
    Static As _Byte i
    Static fX&, fY&, fZ&, fX1&, fY1&, fZ1&, dX!, dY!, n000!, n010!, n001!, n011!, i01!, i11!, n1!
    amplitude = 1: total = 0: maxAmplitude = 0: scale = S
    For i = 0 To O
        tX = X / scale
        tY = Y / scale
        tZ = Z / scale
        fX& = Int(tX)
        fY& = Int(tY)
        fZ& = Int(tZ)
        dX! = fade!(tX - fX&)
        dY! = fade!(tY - fY&)
        fX1& = fX& + 1
        fY1& = fY& + 1
        fZ1& = fZ& + 1
        n000! = perm3(fX&, fY&, fZ&, M)
        n010! = perm3(fX&, fY1&, fZ&, M)
        n001! = perm3(fX&, fY&, fZ1&, M)
        n011! = perm3(fX&, fY1&, fZ1&, M)
        i01! = n000! + (perm3(fX1&, fY&, fZ&, M) - n000!) * dX!
        i11! = n001! + (perm3(fX1&, fY&, fZ1&, M) - n001!) * dX!
        n1! = i01! + (n010! + (perm3(fX1&, fY1&, fZ&, M) - n010!) * dX! - i01!) * dY!
        total = total + amplitude * (n1! + (i11! + (n011! + (perm3(fX1&, fY1&, fZ1&, M) - n011!) * dX! - i11!) * dY! - n1!) * fade!(tZ - fZ&))
        maxAmplitude = maxAmplitude + amplitude
        amplitude = amplitude / 2
        scale = scale / 2
    Next i
    fractal3! = total / maxAmplitude
End Function
$Checking:On
